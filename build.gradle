plugins {
    id 'java-library'
    id 'java'
    id "io.qameta.allure" version "2.9.6"
}

group 'io.github.sskorol'
version '1.0.0'
description 'Test Data Supplier example'

ext {
    aspectjVersion = '1.9.9.1'
    lombokVersion = '1.18.24'
}

configurations {
    agent
}

java {
    sourceCompatibility = 17
    targetCompatibility = 17
}

repositories {
    mavenCentral()
    mavenLocal()
}

dependencies {
    agent "org.aspectj:aspectjweaver:${aspectjVersion}"
    implementation('org.assertj:assertj-core:3.22.0',
            'org.testng:testng:7.6.0',
            'io.github.sskorol:test-data-supplier:2.1.0',
            'io.qameta.allure:allure-testng:2.18.1',
            'org.slf4j:slf4j-simple:2.0.0-alpha7',
            "org.aspectj:aspectjrt:${aspectjVersion}",
            "org.aspectj:aspectjweaver:${aspectjVersion}",
            "org.projectlombok:lombok:${lombokVersion}"
    )
    annotationProcessor("org.projectlombok:lombok:$lombokVersion")
    testImplementation("org.projectlombok:lombok:$lombokVersion")
    testAnnotationProcessor("org.projectlombok:lombok:$lombokVersion")
}

[compileJava, compileTestJava]*.options*.compilerArgs = ['-parameters']

task copyConfig(type: Copy) {
    from 'src/test/resources/config'
    into 'build/allure-results'
}

task copyHistory(type: Copy) {
    from 'src/test/resources/history'
    into 'build/allure-results/history'
}

test {
    testLogging {
        events "skipped", "failed"
        exceptionFormat "full"
    }

    useTestNG() {
        listeners << 'io.github.sskorol.listeners.BaseListener'
        suites 'src/test/resources/suites/parent.xml'
    }

    systemProperty 'allure.model.indentOutput', true
    systemProperty 'allure.results.directory', 'build/allure-results'
    systemProperty 'allure.link.issue.pattern', 'https://github.com/sskorol/test-data-supplier-gradle-example/issues/{}'
    systemProperty 'allure.link.tms.pattern', 'https://github.com/sskorol/test-data-supplier-gradle-example/issues/{}'
}

test.dependsOn copyConfig, copyHistory

allure {
    autoconfigure = true
    version = '2.18.1'
}

wrapper {
    gradleVersion = '7.4.2'
}
