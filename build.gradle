plugins {
    id 'java-library'
    id 'java'
    id "io.qameta.allure" version "2.11.2"
}

group 'io.github.sskorol'
version '1.0.0'
description 'Test Data Supplier example'

ext {
    aspectjVersion = '1.9.19'
    lombokVersion = '1.18.26'
}

configurations {
    agent
}

java {
    sourceCompatibility = 17
    targetCompatibility = 17
}

repositories {
    mavenCentral()
    mavenLocal()
}

dependencies {
    agent "org.aspectj:aspectjweaver:${aspectjVersion}"
    implementation('org.assertj:assertj-core:3.24.2',
            'org.testng:testng:7.7.1',
            'io.github.sskorol:test-data-supplier:2.2.0',
            'io.qameta.allure:allure-testng:2.21.0',
            'org.slf4j:slf4j-simple:2.0.6',
            "org.aspectj:aspectjrt:${aspectjVersion}",
            "org.aspectj:aspectjweaver:${aspectjVersion}",
            "org.projectlombok:lombok:${lombokVersion}"
    )
    annotationProcessor("org.projectlombok:lombok:$lombokVersion")
    testImplementation("org.projectlombok:lombok:$lombokVersion")
    testAnnotationProcessor("org.projectlombok:lombok:$lombokVersion")
}

[compileJava, compileTestJava]*.options*.compilerArgs = ['-parameters']

tasks.register('copyConfig', Copy) {
    from 'src/test/resources/config'
    into 'build/allure-results'
}

tasks.register('copyHistory', Copy) {
    from 'src/test/resources/history'
    into 'build/allure-results/history'
}

test {
    doFirst {
        jvmArgs(
                "-javaagent:${configurations.agent.singleFile}",
                '--add-opens', 'java.base/java.lang=ALL-UNNAMED'
        )
    }

    testLogging {
        events "skipped", "failed"
        exceptionFormat "full"
    }

    useTestNG() {
        listeners << 'io.github.sskorol.listeners.BaseListener'
        suites 'src/test/resources/suites/parent.xml'
    }

    systemProperty 'allure.model.indentOutput', true
    systemProperty 'allure.results.directory', 'build/allure-results'
    systemProperty 'allure.link.issue.pattern', 'https://github.com/sskorol/test-data-supplier-gradle-example/issues/{}'
    systemProperty 'allure.link.tms.pattern', 'https://github.com/sskorol/test-data-supplier-gradle-example/issues/{}'
}

test.dependsOn copyConfig, copyHistory

allure {
    version = '2.21.0'
}

wrapper {
    gradleVersion = '8.0.1'
}
